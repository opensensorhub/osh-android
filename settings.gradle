rootProject.name = 'osh-android'
def oshMainDir = "$rootDir/.."

// include builds from other repos
def repos = [
    'osh-core': [
        'lib-ogc/swe-common-core',
        'lib-ogc/swe-common-om',
        'lib-ogc/sensorml-core',
        'lib-ogc/ogc-services-common',
        'lib-ogc/ogc-services-swe',
        'lib-ogc/ogc-services-sos',
        'lib-ogc/ogc-services-sps',
        'lib-ogc/sensorml-core',
        'lib-ogc/sensorml-core',
        'sensorhub-core',
        'sensorhub-service-swe',
        'sensorhub-storage-perst',

        'sensorhub-utils-kryo',
        'sensorhub-datastore-h2'
    ],
    'osh-addons': [
        'persistence/sensorhub-storage-compat',
        'persistence/sensorhub-storage-h2',
        'comm/sensorhub-comm-ble',
        'comm/sensorhub-comm-ble-dbus',
        'sensors/health/sensorhub-driver-angelsensor',
        'sensors/video/sensorhub-driver-videocam',
        'services/sensorhub-service-video',
        'sensors/positioning/sensorhub-driver-trupulse',
        'processing/sensorhub-process-vecmath',
//        'processing/sensorhub-process-geoloc'
    ]
]

repos.each { repoName, modules ->
  File repoDir = new File(oshMainDir, repoName).getCanonicalFile()
  if (!repoDir.exists())
    throw new GradleException('Repository ' + repoName + ' is missing. Please clone it from https://github.com/opensensorhub/' + repoName)
  modules.each { moduleName ->
    File moduleDir = new File(repoDir, moduleName).getCanonicalFile()
    // include all folders with build.gradle as subprojects
    FileTree subprojects = fileTree(moduleDir).include('**/build.gradle')
    subprojects.files.each { File f ->
      File projectFolder = f.parentFile
      String projectName = projectFolder.name
      include projectName
      project(':' + projectName).projectDir = projectFolder
    }
  }
}

// include all folders with build.gradle as subprojects
FileTree subprojects = fileTree(rootDir).include('**/build.gradle')
subprojects.files.each { File f ->
  File projectFolder = f.parentFile
  if (projectFolder != rootDir) {
    String projectName = projectFolder.name
    include projectName
    project(':' + projectName).projectDir = projectFolder
  }
}

